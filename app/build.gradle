ext {
    android_support_version = '25.2.0'
    appId = "com.jmstudios.redmoon"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


if(rootProject.file("keystore.properties").exists()) {
    // From https://developer.android.com/studio/publish/app-signing.html
    // to load the signing config file
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
   }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId appId
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 28
        versionName "2.10.2"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ eventBusIndex : 'com.jmstudios.redmoon.event.EventBusIndex' ]
            }
        }
        resValue "string", "applicationId", appId
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "applicationId", "$appId$applicationIdSuffix"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'NewApi'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$android_support_version"
    compile "com.android.support:design:$android_support_version"
    compile 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    compile 'com.github.paolorotolo:appintro:3.4.0'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

repositories {
    mavenCentral()
}
